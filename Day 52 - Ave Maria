rule yara_for_detecting_ave_maria
{
  meta:
    author = "Denice"
    description = "Yara rule for detecting Ave Maria"
    hash = "f65a8af1100b56f2ebe014caeaa5bb2fbbca2da76cb99f3142354e31fbba5c8c"
  
  strings:
    $str1 = "Wow64DisableWow64FsRedirection"
    $str2 = "Wow64RevertWow64FsRedirection"
    $str3 = "DPAPI"
    $str4 = "MaxConnectionsPer1_0Server"
    $str5 = "MaxConnectionsPerServer"
    $str6 = "%SystemRoot%\\System32\\termsrv.dll" wide
    $str7 = "for /F \"usebackq tokens=*\" %%A in ("
    $str8 = "Elevation:Administrator!new:{3ad05575-8857-4850-9277-11b85bdb8e09}"
    $str9 = "/n:%temp%\\ellocnak.xml"
    $cmd1 = "cmd.exe /C ping 1.2.3.4 -n 2 -w 1000 > Nul & Del /f /q "
    $cmd2 = "cmd.exe /C C:\\Windows\\System32\\sdclt.exe"
    $ps1 = "powershell Add-MpPreference -ExclusionPath "
    $path2 = "software\\Aerofox\\FoxmailPreview"
    $path3 = "Software\\Classes\\Folder\\shell\\open\\command"
  
  condition:
    uint16be(0) == 0x4D5A and
    5 of ($str*) and
    all of ($cmd*) and
    $ps1 and
    all of ($path*)
}
