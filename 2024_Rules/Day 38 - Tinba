import "pe"
rule detects_tinba
{
    meta:
      author = "Denice"
      description = "Rule to detect Tinba binary. Weird DLLs"
      hash = "D9490B53A895B3B78F5754F7C28A854A2E077A1F4D69B3578415D9258B5BF395"
    
    strings:
      $str1 = "LancetInexplicableInability.exe"
      $str2 = "<PrivateImplementationDetails>{8C835CC6-9E8D-4983-94EE-A177F4B43ED8}"
      $d1 = "MundanePervades.dll"
      $d2 = "ImpingementMiscounted.dll"
      $d3 = "LogoffOscillosco.dll"
      $d4 = "HopelessnessInseminati.dll"
      $d5 = "HeronMisspend.dll"
      $x = { 00 20 1c 04 00 00 0a 06 }
            /* PeteringInfluences::IsostaticHumanoids::MaltreatedIncarcerat();
            0x00409930      add     byte [eax], ah
            0x00409932      enter   7, 0       ; 7
            0x00409936      or      al, byte [esi]
            0x00409938      push    es
            0x00409939      pop     eax
            0x0040993a      push    0x2a0a
            0x0040993f      add     ah, bh
            0x00409941      xlatb
            0x00409942      push    es
            0x00409943      cld
            */
      $x1 = { a3 95 95 d7 d7 d7 b1 b1 b1 b1 d7 95 95 ad ?? }
            /* ;-- str.tnnPPP________Pnn:
            0x0043161b          .string "tnnPPP\xc2\xa3\xc2\xa3\xc2\xa3\xc2\xa3Pn" ; len=16
            0x0043162b      aaa
            0x0043162c      dec     edx
            0x0043162d      cmpsb   byte [esi], byte ptr es:[edi]
            0x0043162e      sahf
            */

    condition:
      uint16be(0) == 0x4D5A and
      pe.imports("mscoree.dll", "_CorExeMain") and
      all of them

}

